subprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'
	apply plugin: 'checkstyle'
	apply plugin: 'findbugs'

	sourceCompatibility = 1.8

	repositories { mavenCentral() }

	dependencies { testCompile 'junit:junit:4.8.2' }

	version = '1.0.0-SNAPSHOT'
	group = 'rocks.cta'

	jar { manifest.attributes 'Version': version }

	checkstyle {
		ignoreFailures = false
		configFile = new File("${projectDir}/../config/checkstyle/cta-checkstyle.xml")
	}

	findbugs {
		ignoreFailures = false
		excludeFilter = new File("${projectDir}/../config/findbugs/findBugsExcludeFilter.xml")
		includeFilter = new File("${projectDir}/../config/findbugs/findBugsIncludeFilter.xml")
	}

	def nexusSnapshotUrl = project.hasProperty('nexusSnapshotUrl') ? project['nexusSnapshotUrl'] : ''
	def nexusReleaseUrl = project.hasProperty('nexusReleaseUrl') ? project['nexusReleaseUrl'] : ''
	def nexusUsername = project.hasProperty('nexusUsername') ? project['nexusUsername'] : ''
	def nexusPassword = project.hasProperty('nexusPassword') ? project['nexusPassword'] : ''

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "${nexusReleaseUrl}") {
					authentication(userName: "${nexusUsername}", password: "${nexusPassword}")
				}
				snapshotRepository(url: "${nexusSnapshotUrl}") {
					authentication(userName: "${nexusUsername}", password: "${nexusPassword}")
				}
				pom.version = "${project.version}"
				pom.artifactId = "${project.name}"
				pom.groupId = "${project.group}"
			}
		}
	}

	task checkProperties << {
		if (!nexusUsername || !nexusPassword || !nexusReleaseUrl  || !nexusSnapshotUrl ) {
			throw new IllegalArgumentException("Cannot perform uploadArchives task due to missing repository settings!\n" +
			"Please specify the following properties: nexusReleaseUrl, nexusSnapshotUrl, nexusUsername, nexusPassword");
		}
	}

	uploadArchives.dependsOn checkProperties


}


task wrapper(type: Wrapper) { gradleVersion = '2.4' }
